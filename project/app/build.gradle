plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "tech.sud.mgp.hello"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode Integer.parseInt(project.versionCode)
        versionName project.versionName
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        applicationVariants.all { variant ->
            variant.outputs.all {
                def temNewFileName = "${variant.flavorName}-${project.baseUrlConfig}-${project.versionName}-${project.versionCode}-${variant.buildType.name}"
                // 修改apk名字
                outputFileName = "HellodSud-Android-" + temNewFileName + ".apk"
            }
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty("jksPropertyPath") && project.jksPropertyPath != "") {
                // release设置发布签名路径
                Properties properties = new Properties()
                properties.load(new FileInputStream(file(project.jksPropertyPath)))
                storeFile file(properties['STORE_FILE'])
                storePassword properties['STORE_PWD']
                keyAlias properties['KEY_ALIAS']
                keyPassword properties['KEY_PWD']
            } else {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        debug {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs/zego', 'libs/agora']
        }
    }
    flavorDimensions 'channel'
    productFlavors {
        official {
            String channel = "official"
            buildConfigField("String", "CHANNEL", "\"" + channel + "\"")
            buildConfigField("String", "baseUrl", "\"" + project.baseUrl + "\"")
            buildConfigField("String", "interactBaseUrl", "\"" + project.interactBaseUrl + "\"")
            buildConfigField("String", "gameBaseUrl", "\"" + project.gameBaseUrl + "\"")
            buildConfigField("String", "buglyAppId", "\"" + project.buglyAppId + "\"")
            buildConfigField("boolean", "gameIsTestEnv", project.gameIsTestEnv)
        }
    }
}

dependencies {

    implementation fileTree(include: ['*.jar', "*.aar", '*.so'], dir: 'libs/zego')
    implementation fileTree(include: ['*.jar', "*.aar", '*.so'], dir: 'libs/agora')
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // 引入common工程依赖
    implementation project(':SudMGPSDK')

    // 屏幕适配：https://github.com/JessYanCoding/AndroidAutoSize
    implementation 'com.github.JessYanCoding:AndroidAutoSize:v1.2.1'

    // 腾讯bugly奔溃统计
    implementation 'com.tencent.bugly:crashreport:4.0.0'

    // 依赖okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'

    // 工具类库: https://github.com/Blankj/AndroidUtilCode
    implementation 'com.blankj:utilcodex:1.31.0'

    // 图片加载库
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // 网络请求库
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.trello.rxlifecycle4:rxlifecycle:4.0.2'
    implementation 'com.trello.rxlifecycle4:rxlifecycle-components:4.0.2'

    // RecyclerView的Adapter: https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'

    // 增加mp4 sdk
    implementation 'com.github.bytedance:AlphaPlayer:1.2.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.9.6'
    // Android svga https://github.com/svga/SVGAPlayer-Android
    implementation 'com.github.yyued:SVGAPlayer-Android:2.6.1'
    // 播放json动画的库，网址为：https://github.com/airbnb/lottie-android
    implementation "com.airbnb.android:lottie:3.7.0"
    // Glide支持webp动图的库
    implementation "com.github.zjupure:webpdecoder:2.0.4.12.0"

    // 沉浸式状态栏
    implementation "com.gyf.immersionbar:immersionbar:3.0.0"
    // 事件通知
    implementation 'io.github.jeremyliao:live-event-bus:1.8.0'

    // RongCloud IM
    implementation 'cn.rongcloud.sdk:im_lib:5.2.0'
    // RongCloud IM
    implementation 'cn.rongcloud.sdk:rtc_lib:5.2.0'
    // RongCloud IM 语聊房
    implementation 'cn.rongcloud.sdk:voiceroom_lib:2.0.8'
}